@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

#### Required dependencies  ####
find_dependency(Boost REQUIRED COMPONENTS program_options filesystem graph serialization)
find_dependency(DGtal REQUIRED 1.0)

#### Optional dependencies based on SGEXT options ####
if(@SG_REQUIRES_ITK@) #if(${SG_REQUIRES_ITK})
  find_dependency(ITK REQUIRED COMPONENTS
    @itk_components@
    CONFIG)
  include(${ITK_USE_FILE})
endif()
if(@SG_MODULE_VISUALIZE_WITH_QT@) #if (${SG_MODULE_VISUALIZE_WITH_QT})
  find_dependency(Qt5 REQUIRED
    Widgets
    Xml
    OpenGL)
endif()
if(@SG_REQUIRES_VTK@) # if(${SG_REQUIRES_VTK})
  find_dependency(VTK COMPONENTS
    @vtk_components@
    CONFIG
    REQUIRED
    )
endif()
# TBB is optionally used in modules/generate, only for GNU
if(@SG_REQUIRES_TBB@) # if(${SG_REQUIRES_TBB})
  set(OLD_CMAKE_FIND_PACKAGE_PREFER_CONFIG ${CMAKE_FIND_PACKAGE_PREFER_CONFIG})
  find_dependency(TBB)
  set(CMAKE_FIND_PACKAGE_PREFER_CONFIG ${OLD_CMAKE_FIND_PACKAGE_PREFER_CONFIG})
endif()

get_filename_component(SGEXT_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
if(NOT TARGET SGCore)
  include ("${SGEXT_CMAKE_DIR}/SGEXTTargets.cmake")
endif()
